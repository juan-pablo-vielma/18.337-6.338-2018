{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, PolyJuMP, MultivariatePolynomials, DynamicPolynomials, SumOfSquares\n",
    "\n",
    "type Box\n",
    "    xl::Float64\n",
    "    xu::Float64\n",
    "    yl::Float64\n",
    "    yu::Float64\n",
    "end\n",
    "\n",
    "function solveModel(npieces,degree,boxes::Array{Box},domain::Box,startcondition,endcondition,misdpsolver,tol_int = 1e-9, tol_feas = 1e-9, tol_gap = 1e-6)\n",
    "\n",
    "    model = SOSModel(solver=misdpsolver)\n",
    "\n",
    "    tol_nonneg = tol_feas # Tolerance for polynomial nonnegativity\n",
    "\n",
    "    (X₀, X₀′, X₀′′) = startcondition\n",
    "    (X₁, X₁′, X₁′′) = endcondition\n",
    "\n",
    "    # Discretize time into npieces+1 times\n",
    "    Tmin = 0.\n",
    "    Tmax = 1.\n",
    "    T = linspace(Tmin, Tmax, npieces+1)\n",
    "\n",
    "    # Polynomials are a function of t\n",
    "    @polyvar(t)\n",
    "    Z = monomials(t, 0:degree)\n",
    "\n",
    "    # Binary variables to choose safe regions\n",
    "    # variables are indexed by trajectory piece (integer 1:npieces) and boxes (of type Box)\n",
    "    @variable(model, H[1:npieces,boxes], Bin)\n",
    "\n",
    "    # Big-M values\n",
    "    (Mxl, Mxu, Myl, Myu) = (domain.xl, domain.xu, domain.yl, domain.yu)\n",
    "    p = Dict()\n",
    "    for j in 1:npieces\n",
    "        @constraint(model, sum(H[j,box] for box in boxes) == 1)\n",
    "\n",
    "        # Polynomial variables = a variable for the coefficients of each monomial in Z\n",
    "        p[(:x,j)] = @variable(model, _, PolyJuMP.Poly(Z), basename=\"px$j\")\n",
    "        p[(:y,j)] = @variable(model, _, PolyJuMP.Poly(Z), basename=\"py$j\")\n",
    "\n",
    "        # Constraints to choose safe region\n",
    "        for box in boxes\n",
    "            xl, xu, yl, yu = box.xl, box.xu, box.yl, box.yu\n",
    "            @assert xl >= Mxl\n",
    "            @constraint(model, p[(:x,j)] >= Mxl + (xl-Mxl)*H[j,box], domain = (@set t >= T[j] && t <= T[j+1]))\n",
    "            @assert xu <= Mxu\n",
    "            @constraint(model, p[(:x,j)] <= Mxu + (xu-Mxu)*H[j,box], domain = (@set t >= T[j] && t <= T[j+1]))\n",
    "            @assert yl >= Myl\n",
    "            @constraint(model, p[(:y,j)] >= Myl + (yl-Myl)*H[j,box], domain = (@set t >= T[j] && t <= T[j+1]))\n",
    "            @assert yu <= Myu\n",
    "            @constraint(model, p[(:y,j)] <= Myu + (yu-Myu)*H[j,box], domain = (@set t >= T[j] && t <= T[j+1]))\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Boundary and interstitial smoothing conditions\n",
    "    for axis in (:x,:y)\n",
    "        @constraint(model,               p[(axis,1)       ](t=>Tmin) == X₀[axis])\n",
    "        @constraint(model, differentiate(p[(axis,1)], t   )(t=>Tmin) == X₀′[axis])\n",
    "        @constraint(model, differentiate(p[(axis,1)], t, 2)(t=>Tmin) == X₀′′[axis])\n",
    "\n",
    "        for j in 1:npieces-1\n",
    "            @constraint(model,               p[(axis,j)       ](t=>T[j+1]) ==               p[(axis,j+1)       ](t=>T[j+1]))\n",
    "            @constraint(model, differentiate(p[(axis,j)], t   )(t=>T[j+1]) == differentiate(p[(axis,j+1)], t   )(t=>T[j+1]))\n",
    "            @constraint(model, differentiate(p[(axis,j)], t, 2)(t=>T[j+1]) == differentiate(p[(axis,j+1)], t, 2)(t=>T[j+1]))\n",
    "        end\n",
    "\n",
    "        @constraint(model, p[(axis,npieces)](t=>Tmax) == X₁[axis])\n",
    "    end\n",
    "\n",
    "    # Objective function\n",
    "    @variable(model, γ[keys(p)] ≥ 0)\n",
    "    for (key,val) in p\n",
    "        @constraint(model, γ[key] ≥ norm(differentiate(val, t, 3)))\n",
    "    end\n",
    "    @objective(model, Min, sum(γ))\n",
    "\n",
    "    solve(model)\n",
    "\n",
    "    # create and return a function that evaluates the piecewise polynomial anwer\n",
    "    PP = Dict(key => getvalue(p[key]) for key in keys(p))\n",
    "    HH = getvalue(H)\n",
    "    function eval_poly(r)\n",
    "        for i in 1:npieces\n",
    "            if T[i] <= r <= T[i+1]\n",
    "                return PP[(:x,i)](t=>r), PP[(:y,i)](t=>r)\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "        error(\"Time $r out of interval [$(minimum(T)),$(maximum(T))]\")\n",
    "    end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "function simpleplot(domain, boxes,eval_poly)\n",
    "    plot(Shape([domain.xl,domain.xu,domain.xu,domain.xl],[domain.yl,domain.yl,domain.yu,domain.yu]),c = [RGB(0,0,1)],linewidth = 0,label=\"\")\n",
    "    for box in boxes\n",
    "        plot!(Shape([box.xl,box.xu,box.xu,box.xl],[box.yl,box.yl,box.yu,box.yu]),c = [RGB(1,1,1)],linewidth = 0,label=\"\")\n",
    "    end\n",
    "    for t in linspace(0, 1, 100)\n",
    "        (x,y) = eval_poly(t)\n",
    "        plot!([x],[y],marker=(:circle ,3, [RGB(1,0,0)]),label=\"\")\n",
    "    end\n",
    "    plot!()\n",
    "end\n",
    "function animatedplot(domain, boxes,eval_poly)\n",
    "    pyplot()\n",
    "    animation = @animate for t in linspace(0, 1, 100)\n",
    "        plot(Shape([domain.xl,domain.xu,domain.xu,domain.xl],[domain.yl,domain.yl,domain.yu,domain.yu]),c = [RGB(0,0,1)],linewidth = 0,label=\"\")\n",
    "        for box in boxes\n",
    "            plot!(Shape([box.xl,box.xu,box.xu,box.xl],[box.yl,box.yl,box.yu,box.yu]),c = [RGB(1,1,1)],linewidth = 0,label=\"\")\n",
    "        end\n",
    "\n",
    "        (x,y) = eval_poly(t)\n",
    "        plot!([x],[y],marker=(:circle ,3, [RGB(1,0,0)]),label=\"\")\n",
    "    end\n",
    "    gif(animation,fps=25)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Define instance\n",
    "npieces = 8 # Number of trajectory pieces\n",
    "degree = 3 # degree of polynomial trajectories\n",
    "boxes = Box[\n",
    "    Box(0.0,1.0,0.0,0.3),\n",
    "    Box(0.8,1.7,0.1,0.3),\n",
    "    Box(1.4,1.9,0.2,0.4),\n",
    "    Box(1.0,1.7,0.3,0.5),\n",
    "    Box(0.5,1.4,0.4,0.6),\n",
    "    Box(0.0,1.0,0.5,0.7),\n",
    "    Box(0.2,1.0,0.6,0.8),\n",
    "    Box(0.5,1.3,0.7,0.9),\n",
    "    Box(1.0,2.0,0.7,1.0),\n",
    "    ]\n",
    "domain = Box(0,2,0,1)\n",
    "# Start location, velocity and acceleration\n",
    "X₀   = Dict(:x=>0, :y=>0)\n",
    "X₀′  = Dict(:x=>1, :y=>0)\n",
    "X₀′′ = Dict(:x=>0, :y=>0)\n",
    "# End location, velocity and acceleration\n",
    "X₁   = Dict(:x=>2, :y=>1)\n",
    "X₁′  = Dict(:x=>1, :y=>0)\n",
    "X₁′′ = Dict(:x=>0, :y=>0)\n",
    "\n",
    "# Setup Pajarito MISDP solver\n",
    "using Pajarito, Mosek, Gurobi #CPLEX\n",
    "MIPsolver = GurobiSolver()\n",
    "#MIPsolver = CplexSolver()\n",
    "SDPsolver = MosekSolver(MSK_IPAR_LOG=0,MSK_DPAR_INTPNT_CO_TOL_PFEAS=1e-6,MSK_DPAR_INTPNT_CO_TOL_DFEAS=1e-6,\n",
    "    MSK_DPAR_INTPNT_CO_TOL_REL_GAP=1e-5,MSK_DPAR_INTPNT_TOL_INFEAS=1e-8,MSK_DPAR_INTPNT_CO_TOL_MU_RED=1e-6)\n",
    "misdpsolver = PajaritoSolver(mip_solver=MIPsolver, mip_subopt_solver=MIPsolver, cont_solver=SDPsolver, mip_solver_drives=true, cut_zero_tol=1e-6, log_level=3, rel_gap=1e-3, solve_relax=false)\n",
    "\n",
    "# Solve\n",
    "tic()\n",
    "eval_poly = solveModel(npieces,degree,boxes,domain,(X₀, X₀′, X₀′′),(X₁, X₁′, X₁′′),misdpsolver)\n",
    "toc()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "eval_poly(0.4)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "simpleplot(domain, boxes,eval_poly)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "animatedplot(domain, boxes,eval_poly)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.2",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
